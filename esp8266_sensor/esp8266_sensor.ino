#include <ESP8266WiFi.h>
#include <PubSubClient.h>
#include <DHT.h>
#include <ArduinoJson.h>
#include <EEPROM.h>


// -----------------------------
// WiFi Ñ– MQTT
const char* ssid = "Ostap";
const char* password = "Ostap2004";

const char* mqtt_server = "28c5545e26094092b6785f4d24098c1f.s1.eu.hivemq.cloud";
const int mqtt_port = 8883;
const char* mqtt_user = "My_Cred";
const char* mqtt_pass = "MyCred1234";

// MQTT topics
const char* temp_topic = "home/temperature";
const char* hum_topic = "home/humidity";
const char* request_topic = "device/request";
const char* response_topic = "device/response";
const char* serial_topic = "device/set_serial";

// -----------------------------
// DHT Ð´Ð°Ñ‚Ñ‡Ð¸Ðº
#define DHTPIN 2
#define DHTTYPE DHT22
DHT dht(DHTPIN, DHTTYPE);

// -----------------------------
WiFiClientSecure espClient;
PubSubClient client(espClient);

// ---------- Callback ----------

String serialNumber = "";

void saveSerialToEEPROM(const String& serial) {
  EEPROM.begin(64);
  for (int i = 0; i < serial.length(); i++) {
    EEPROM.write(i, serial[i]);
  }
  EEPROM.write(serial.length(), '\0'); // Ð¼Ð°Ñ€ÐºÐµÑ€ ÐºÑ–Ð½Ñ†Ñ Ñ€ÑÐ´ÐºÐ°
  EEPROM.commit();
  EEPROM.end();
}

String readSerialFromEEPROM() {
  EEPROM.begin(64);
  char buffer[32];
  for (int i = 0; i < 32; i++) {
    buffer[i] = EEPROM.read(i);
    if (buffer[i] == '\0') break;
  }
  EEPROM.end();
  return String(buffer);
}

void callback(char* topic, byte* payload, unsigned int length) {
  String msg;
  for (unsigned int i = 0; i < length; i++) {
    msg += (char)payload[i];
  }

  Serial.print("MQTT Ð¿Ð¾Ð²Ñ–Ð´Ð¾Ð¼Ð»ÐµÐ½Ð½Ñ [");
  Serial.print(topic);
  Serial.print("]: ");
  Serial.println(msg);

  DynamicJsonDocument doc(256);
  DeserializationError err = deserializeJson(doc, msg);

  if (err) {
    Serial.println("ÐŸÐ¾Ð¼Ð¸Ð»ÐºÐ° Ð¿Ð°Ñ€ÑÐ¸Ð½Ð³Ñƒ JSON");
    return;
  }

  if (String(topic) == request_topic) {
    String user = doc["user"];
    String pass = doc["pass"];

    DynamicJsonDocument response(128);
    if (user == mqtt_user && pass == mqtt_pass) {
      response["status"] = "success";
    } else {
      response["status"] = "fail";
    }

    String resStr;
    serializeJson(response, resStr);
    client.publish(response_topic, resStr.c_str());
    Serial.println("Ð’Ñ–Ð´Ð¿Ð¾Ð²Ñ–Ð´ÑŒ Ð½Ð° ÐºÑ€ÐµÐ´ÐµÐ½ÑˆÐ¸Ð½Ð°Ð»Ð¸: " + resStr);
  }

  else if (String(topic) == serial_topic) {
    String serial = doc["serial"];
    Serial.println("ÐžÑ‚Ñ€Ð¸Ð¼Ð°Ð½Ð¾ ÑÐµÑ€Ñ–Ð¹Ð½Ð¸Ðº: " + serial);
    serialNumber = serial;
    saveSerialToEEPROM(serialNumber);
    Serial.println("Ð¡ÐµÑ€Ñ–Ð¹Ð½Ð¸Ðº Ð·Ð±ÐµÑ€ÐµÐ¶ÐµÐ½Ð¾ Ð² EEPROM");

    // ðŸ”½ ÐÐ°Ð´Ñ–ÑÐ»Ð°Ñ‚Ð¸ Ð¿Ñ–Ð´Ñ‚Ð²ÐµÑ€Ð´Ð¶ÐµÐ½Ð½Ñ Ð½Ð°Ð·Ð°Ð´
    DynamicJsonDocument response(128);
    response["serial"] = serialNumber;
    response["device"] = mqtt_user; // Ð°Ð±Ð¾ Ð±ÑƒÐ´ÑŒ-ÑÐºÐµ ÑƒÐ½Ñ–ÐºÐ°Ð»ÑŒÐ½Ðµ Ñ–Ð¼â€™Ñ
    String resStr;
    serializeJson(response, resStr);
    client.publish(response_topic, resStr.c_str());
  }

  else if (String(topic) == "device/info_request") {
    DynamicJsonDocument info(128);
    info["serial"] = serialNumber;
    info["device"] = mqtt_user;

    String infoStr;
    serializeJson(info, infoStr);
    client.publish("device/info_response", infoStr.c_str());
    Serial.println("ÐÐ°Ð´Ñ–ÑÐ»Ð°Ð½Ð¾ info: " + infoStr);
  }
}


// ---------- WiFi ----------

void setup_wifi() {
  delay(10);
  Serial.println("ÐŸÑ–Ð´ÐºÐ»ÑŽÑ‡ÐµÐ½Ð½Ñ Ð´Ð¾ Wi-Fi...");
  WiFi.begin(ssid, password);

  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.print(".");
  }

  Serial.println("");
  Serial.println("WiFi Ð·'Ñ”Ð´Ð½Ð°Ð½Ð¾");
  Serial.print("IP Ð°Ð´Ñ€ÐµÑÐ°: ");
  Serial.println(WiFi.localIP());
}

// ---------- MQTT ----------

void reconnect() {
  while (!client.connected()) {
    Serial.print("ÐŸÑ–Ð´ÐºÐ»ÑŽÑ‡ÐµÐ½Ð½Ñ Ð´Ð¾ MQTT... ");
    String clientId = "ESP8266Client-" + String(random(0xffff), HEX);

    if (client.connect(clientId.c_str(), mqtt_user, mqtt_pass)) {
      Serial.println("ÑƒÑÐ¿Ñ–ÑˆÐ½Ð¾");

      client.subscribe(request_topic);
      client.subscribe(serial_topic);
      client.subscribe("device/info_request");

    } else {
      Serial.print("ÐŸÐ¾Ð¼Ð¸Ð»ÐºÐ°, rc=");
      Serial.print(client.state());
      Serial.println(" Ð¿Ð¾Ð²Ñ‚Ð¾Ñ€ Ñ‡ÐµÑ€ÐµÐ· 5 ÑÐµÐºÑƒÐ½Ð´");
      delay(5000);
    }
  }
}

// ---------- SETUP & LOOP ----------

void setup() {
  Serial.begin(115200);
  dht.begin();
  setup_wifi();

  espClient.setInsecure(); // Ñ–Ð³Ð½Ð¾Ñ€ÑƒÐ²Ð°Ð½Ð½Ñ ÑÐµÑ€Ñ‚Ð¸Ñ„Ñ–ÐºÐ°Ñ‚Ñƒ SSL
  client.setServer(mqtt_server, mqtt_port);
  client.setCallback(callback);
  
  serialNumber = readSerialFromEEPROM();
  Serial.println("Ð¡ÐµÑ€Ñ–Ð¹Ð½Ð¸Ðº Ð· EEPROM: " + serialNumber);
}

void loop() {
  if (!client.connected()) {
    reconnect();
  }
  client.loop();

  float h = dht.readHumidity();
  float t = dht.readTemperature();

  if (isnan(h) || isnan(t)) {
    Serial.println("ÐŸÐ¾Ð¼Ð¸Ð»ÐºÐ° Ð·Ñ‡Ð¸Ñ‚ÑƒÐ²Ð°Ð½Ð½Ñ Ð· DHT22");
    return;
  }

  Serial.printf("Ð¢ÐµÐ¼Ð¿ÐµÑ€Ð°Ñ‚ÑƒÑ€Ð°: %.1f Â°C | Ð’Ð¾Ð»Ð¾Ð³Ñ–ÑÑ‚ÑŒ: %.1f %%\n", t, h);

  client.publish(temp_topic, String(t, 1).c_str(), true);
  client.publish(hum_topic, String(h, 1).c_str(), true);

  delay(5000);
}
